# Copyright (c) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("../hidumper.gni")

config("interface_include") {
  include_dirs = [
    "${hidumper_interface}/innerkits/include",
    "${hidumper_interface}/native/innerkits/include/",
  ]
}

config("service_config") {
  include_dirs = [ "native/include" ]
}

config("zidl_config") {
  include_dirs = [ "zidl/include" ]
}

config("zidl_cpu_config") {
  include_dirs = [
    "zidl/include",
    "${hidumper_frameworks_path}/include",
  ]
}

config("dump_cpu_config") {
  visibility = [ "*:*" ]

  include_dirs = [
    "native/include",
    "${hidumper_frameworks_path}/include",
    "${hidumper_interface}/innerkits/include",
    "zidl/include",
    "${hidumper_utils_path}/native/include",
    "${hidumper_frameworks_path}",
  ]
}

ohos_source_set("zidl_client") {
  sources = [
    "zidl/src/dump_broker_cpu_proxy.cpp",
    "zidl/src/dump_broker_proxy.cpp",
  ]

  configs = [
    "${hidumper_utils_path}:utils_config",
    ":interface_include",
    "${hidumper_service_path}:zidl_cpu_config",
  ]

  deps = [ "${hidumper_utils_path}:utils" ]

  external_deps = [
    "c_utils:utils",
    "hilog_native:libhilog",
    "hisysevent_native:libhisysevent",
    "ipc:ipc_core",
    "safwk:system_ability_fwk",
    "samgr:samgr_proxy",
  ]

  subsystem_name = "${hidumper_subsystem_name}"

  part_name = "${hidumper_part_name}"
}

ohos_source_set("zidl_service") {
  sources = [ "zidl/src/dump_broker_stub.cpp" ]

  configs = [
    "${hidumper_utils_path}:utils_config",
    ":interface_include",
    "${hidumper_service_path}:zidl_config",
  ]

  deps = [ "${hidumper_utils_path}:utils" ]

  external_deps = [
    "c_utils:utils",
    "hilog_native:libhilog",
    "hisysevent_native:libhisysevent",
    "ipc:ipc_core",
    "safwk:system_ability_fwk",
    "samgr:samgr_proxy",
  ]

  subsystem_name = "${hidumper_subsystem_name}"

  part_name = "${hidumper_part_name}"
}

ohos_source_set("zidl_cpu_service") {
  sources = [ "zidl/src/dump_broker_cpu_stub.cpp" ]

  configs = [
    "${hidumper_utils_path}:utils_config",
    ":interface_include",
    ":service_config",
    "${hidumper_service_path}:zidl_cpu_config",
  ]

  deps = [ "${hidumper_utils_path}:utils" ]

  external_deps = [
    "c_utils:utils",
    "hilog_native:libhilog",
    "hisysevent_native:libhisysevent",
    "ipc:ipc_core",
    "safwk:system_ability_fwk",
    "samgr:samgr_proxy",
  ]

  subsystem_name = "${hidumper_subsystem_name}"

  part_name = "${hidumper_part_name}"
}

ohos_source_set("hidumper_client") {
  sources = [
    "native/src/dump_cpu_data.cpp",
    "native/src/dump_manager_client.cpp",
    "native/src/dump_manager_cpu_client.cpp",
    "native/src/dump_on_demand_load.cpp",
  ]

  configs = [
    "${hidumper_utils_path}:utils_config",
    ":interface_include",
    "${hidumper_frameworks_path}:hidumper_include",
    "${hidumper_service_path}:service_config",
    "${hidumper_service_path}:zidl_config",
  ]

  deps = [
    "${hidumper_service_path}:zidl_client",
    "${hidumper_utils_path}:utils",
  ]

  external_deps = [
    "c_utils:utils",
    "hilog_native:libhilog",
    "ipc:ipc_core",
    "samgr:samgr_proxy",
  ]

  subsystem_name = "${hidumper_subsystem_name}"

  part_name = "${hidumper_part_name}"
}

ohos_source_set("hidumperservice_source") {
  sources = [
    "native/src/dump_common_utils.cpp",
    "native/src/dump_cpu_data.cpp",
    "native/src/dump_log_manager.cpp",
    "native/src/dump_manager_cpu_client.cpp",
    "native/src/dump_manager_service.cpp",
    "native/src/dump_on_demand_load.cpp",
    "native/src/raw_param.cpp",
    "zidl/src/dump_broker_cpu_proxy.cpp",
  ]

  configs = [
    "${hidumper_utils_path}:utils_config",
    ":interface_include",
    "${hidumper_frameworks_path}:hidumper_include",
    "${hidumper_service_path}:service_config",
    "${hidumper_service_path}:zidl_config",
  ]

  deps = [
    "${hidumper_frameworks_path}:dump_main",
    "${hidumper_service_path}:zidl_service",
    "${hidumper_utils_path}:utils",
  ]

  external_deps = [
    "access_token:libaccesstoken_sdk",
    "access_token:libtoken_setproc",
    "c_utils:utils",
    "eventhandler:libeventhandler",
    "hilog_native:libhilog",
    "hisysevent_native:libhisysevent",
    "ipc:ipc_core",
    "safwk:system_ability_fwk",
    "samgr:samgr_proxy",
  ]

  subsystem_name = "${hidumper_subsystem_name}"

  part_name = "${hidumper_part_name}"
}

ohos_source_set("hidumperservice_cpu_source") {
  public_configs = [ ":dump_cpu_config" ]
  sources = [
    "native/src/dump_app_state_observer.cpp",
    "native/src/dump_battery_stats_subscriber.cpp",
    "native/src/dump_event_handler.cpp",
    "native/src/dump_manager_cpu_service.cpp",
  ]

  configs = [
    "${hidumper_utils_path}:utils_config",
    ":interface_include",
    "${hidumper_frameworks_path}:hidumper_include",
    "${hidumper_service_path}:service_config",
    "${hidumper_service_path}:zidl_config",
  ]

  deps = [
    "${hidumper_service_path}:hidumperservice_source",
    "${hidumper_service_path}:zidl_cpu_service",
  ]

  external_deps = [
    "c_utils:utils",
    "eventhandler:libeventhandler",
    "hilog_native:libhilog",
    "ipc:ipc_core",
    "safwk:system_ability_fwk",
    "samgr:samgr_proxy",
  ]
  defines = []
  if (hidumper_ablility_base_enable) {
    external_deps += [ "ability_base:want" ]
    external_deps += [ "ability_runtime:app_manager" ]
    defines += [ "HIDUMPER_ABILITY_BASE_ENABLE" ]
  }

  if (hidumper_battery_manager_enable && hidumper_common_event_enable) {
    external_deps += [ "battery_manager:batterysrv_client" ]
    external_deps += [ "common_event_service:cesfwk_innerkits" ]
    defines += [ "HIDUMPER_BATTERY_ENABLE" ]
  }

  subsystem_name = "${hidumper_subsystem_name}"

  part_name = "${hidumper_part_name}"
}

ohos_source_set("hidumperservice_cpu_source_test") {
  sources = [
    "native/src/dump_app_state_observer.cpp",
    "native/src/dump_battery_stats_subscriber.cpp",
    "native/src/dump_event_handler.cpp",
    "native/src/dump_manager_cpu_service.cpp",
  ]

  configs = [
    "${hidumper_utils_path}:utils_config",
    ":interface_include",
    "${hidumper_frameworks_path}:hidumper_include",
    "${hidumper_service_path}:service_config",
    "${hidumper_service_path}:zidl_config",
  ]

  deps = [ "${hidumper_service_path}:zidl_cpu_service" ]

  external_deps = [
    "c_utils:utils",
    "eventhandler:libeventhandler",
    "hilog_native:libhilog",
    "safwk:system_ability_fwk",
    "samgr:samgr_proxy",
  ]
  defines = []
  if (hidumper_ablility_base_enable) {
    external_deps += [ "ability_base:want" ]
    external_deps += [ "ability_runtime:app_manager" ]
    defines += [ "HIDUMPER_ABILITY_BASE_ENABLE" ]
  }

  if (hidumper_battery_manager_enable && hidumper_common_event_enable) {
    external_deps += [ "battery_manager:batterysrv_client" ]
    external_deps += [ "common_event_service:cesfwk_innerkits" ]
    defines += [ "HIDUMPER_BATTERY_ENABLE" ]
  }

  subsystem_name = "${hidumper_subsystem_name}"

  part_name = "${hidumper_part_name}"
}

ohos_shared_library("hidumperservice") {
  deps = [ ":hidumperservice_source" ]
  install_enable = true
  version_script = "hidumper.map"
  shlib_type = "sa"
  subsystem_name = "${hidumper_subsystem_name}"
  part_name = "${hidumper_part_name}"
}

ohos_shared_library("hidumpercpuservice") {
  deps = [ ":hidumperservice_cpu_source" ]
  install_enable = true
  version_script = "hidumper.map"
  shlib_type = "sa"
  subsystem_name = "${hidumper_subsystem_name}"
  part_name = "${hidumper_part_name}"
}

############################################################################

ohos_prebuilt_etc("hidumper_service.rc") {
  source = "native/etc/hidumper_service.cfg"

  relative_install_dir = "init"

  subsystem_name = "${hidumper_subsystem_name}"

  part_name = "${hidumper_part_name}"
}

ohos_prebuilt_etc("infos_config") {
  source = "native/etc/infos_config.json"

  relative_install_dir = "hidumper"

  subsystem_name = "${hidumper_subsystem_name}"

  part_name = "${hidumper_part_name}"
}
