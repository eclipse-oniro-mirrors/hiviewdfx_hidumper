# Copyright (c) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("../../hidumper.gni")

config("hidumper_include") {
  include_dirs = [
    ".",
    "${hidumper_frameworks_path}/include",
  ]
}

ohos_source_set("dump_main") {
  sources = [
    "dump_utils.cpp",
    "src/common/dump_cfg.cpp",
    "src/common/dumper_opts.cpp",
    "src/common/dumper_parameter.cpp",
    "src/common/option_args.cpp",
    "src/executor/api_dumper.cpp",
    "src/executor/cmd_dumper.cpp",
    "src/executor/column_rows_filter.cpp",
    "src/executor/cpu_dumper.cpp",
    "src/executor/dumper_group.cpp",
    "src/executor/env_param_dumper.cpp",
    "src/executor/fd_output.cpp",
    "src/executor/file_format_dump_filter.cpp",
    "src/executor/file_stream_dumper.cpp",
    "src/executor/hidumper_executor.cpp",
    "src/executor/ipc_stat_dumper.cpp",
    "src/executor/jsheap_memory_dumper.cpp",
    "src/executor/list_dumper.cpp",
    "src/executor/memory/dma_info.cpp",
    "src/executor/memory/dump_jsheap_info.cpp",
    "src/executor/memory/get_cma_info.cpp",
    "src/executor/memory/get_hardware_info.cpp",
    "src/executor/memory/get_heap_info.cpp",
    "src/executor/memory/get_kernel_info.cpp",
    "src/executor/memory/get_process_info.cpp",
    "src/executor/memory/get_ram_info.cpp",
    "src/executor/memory/memory_filter.cpp",
    "src/executor/memory/memory_info.cpp",
    "src/executor/memory/memory_util.cpp",
    "src/executor/memory/parse/parse_meminfo.cpp",
    "src/executor/memory/parse/parse_smaps_info.cpp",
    "src/executor/memory/parse/parse_smaps_rollup_info.cpp",
    "src/executor/memory/parse/parse_vmallocinfo.cpp",
    "src/executor/memory/smaps_memory_info.cpp",
    "src/executor/memory_dumper.cpp",
    "src/executor/properties_dumper.cpp",
    "src/executor/sa_dumper.cpp",
    "src/executor/traffic_dumper.cpp",
    "src/executor/version_dumper.cpp",
    "src/executor/zip_output.cpp",
    "src/executor/zipfolder_output.cpp",
    "src/factory/api_dumper_factory.cpp",
    "src/factory/cmd_dumper_factory.cpp",
    "src/factory/column_rows_filter_factory.cpp",
    "src/factory/cpu_dumper_factory.cpp",
    "src/factory/dumper_group_factory.cpp",
    "src/factory/env_param_dumper_factory.cpp",
    "src/factory/fd_output_factory.cpp",
    "src/factory/file_dumper_factory.cpp",
    "src/factory/file_format_dump_filter_factory.cpp",
    "src/factory/ipc_stat_dumper_factory.cpp",
    "src/factory/jsheap_memory_dumper_factory.cpp",
    "src/factory/list_dumper_factory.cpp",
    "src/factory/memory_dumper_factory.cpp",
    "src/factory/properties_dumper_factory.cpp",
    "src/factory/sa_dumper_factory.cpp",
    "src/factory/traffic_dumper_factory.cpp",
    "src/factory/version_dumper_factory.cpp",
    "src/factory/zip_output_factory.cpp",
    "src/manager/dump_implement.cpp",
    "src/util/config_data.cpp",
    "src/util/config_utils.cpp",
    "src/util/dump_compressor.cpp",
    "src/util/file_utils.cpp",
    "src/util/string_utils.cpp",
    "src/util/zip/zip_writer.cpp",
    "src/util/zip_utils.cpp",
  ]

  configs = [
    "${hidumper_utils_path}:utils_config",
    "${hidumper_service_path}:interface_include",
    "${hidumper_service_path}:zidl_config",
    "${hidumper_service_path}:service_config",
    ":hidumper_include",
  ]

  deps = [ "${hidumper_utils_path}:utils" ]

  external_deps = [
    "ability_base:base",
    "ability_base:zuri",
    "c_utils:utils",
    "drivers_interface_memorytracker:libmemorytracker_proxy_1.0",
    "eventhandler:libeventhandler",
    "hdf_core:libhdf_utils",
    "hilog:libhilog",
    "init:libbegetutil",
    "ipc:ipc_core",
    "json:nlohmann_json_static",
    "safwk:system_ability_fwk",
    "samgr:samgr_proxy",
    "zlib:shared_libz",
  ]
  defines = []
  if (hidumper_graphic_enable) {
    external_deps += [
      "graphic_2d:2d_graphics_new",
      "graphic_2d:libcomposer",
      "graphic_2d:librender_service_base",
      "graphic_2d:librender_service_client",
      "graphic_surface:surface",
      "image_framework:image_native",
    ]
    defines += [
      "HIDUMPER_GRAPHIC_ENABLE",
      "ROSEN_CROSS_PLATFORM",
    ]
  }

  if (hidumper_ability_runtime_enable) {
    external_deps += [ "ability_runtime:app_manager" ]
    defines += [ "HIDUMPER_ABILITY_RUNTIME_ENABLE" ]
  }

  if (hidumper_netmanager_base_enable) {
    external_deps += [ "netmanager_base:net_stats_manager_if" ]
    defines += [ "HIDUMPER_NETMANAGER_BASE_ENABLE" ]
  }

  if (hidumper_bundlemanager_framework_enable) {
    external_deps += [
      "bundle_framework:appexecfwk_base",
      "bundle_framework:appexecfwk_core",
    ]
    defines += [ "HIDUMPER_BUNDLEMANAGER_FRAMEWORK_ENABLE" ]
  }

  if (hidumper_hiviewdfx_hisysevent_enable) {
    external_deps += [ "hisysevent:libhisysevent" ]
    defines += [ "HIDUMPER_HIVIEWDFX_HISYSEVENT_ENABLE" ]
  }

  subsystem_name = "hiviewdfx"

  part_name = "hidumper"
}

ohos_shared_library("hidumperclient") {
  install_enable = true
  sources = [
    "${hidumper_client_path}/native/dump_client_main.cpp",
    "dump_utils.cpp",
  ]

  configs = [
    "${hidumper_utils_path}:utils_config",
    "${hidumper_service_path}:interface_include",
    "${hidumper_service_path}:zidl_config",
    "${hidumper_service_path}:service_config",
    ":hidumper_include",
  ]

  deps = [
    "${hidumper_service_path}:hidumper_client",
    "${hidumper_utils_path}:utils",
  ]

  external_deps = [
    "c_utils:utils",
    "eventhandler:libeventhandler",
    "hilog:libhilog",
    "init:libbegetutil",
    "ipc:ipc_core",
    "safwk:system_ability_fwk",
    "samgr:samgr_proxy",
  ]

  subsystem_name = "hiviewdfx"

  part_name = "hidumper"
}

ohos_executable("hidumper") {
  install_enable = true

  sources = [ "${hidumper_client_path}/native/main.cpp" ]

  configs = [
    "${hidumper_utils_path}:utils_config",
    "${hidumper_service_path}:interface_include",
    "${hidumper_service_path}:zidl_config",
    "${hidumper_service_path}:service_config",
    ":hidumper_include",
  ]

  deps = [
    ":hidumperclient",
    "${hidumper_service_path}:hidumper_client",
    "${hidumper_utils_path}:utils",
  ]

  external_deps = [
    "c_utils:utils",
    "hilog:libhilog",
    "ipc:ipc_core",
    "safwk:system_ability_fwk",
    "samgr:samgr_proxy",
  ]

  subsystem_name = "hiviewdfx"

  part_name = "hidumper"
}
